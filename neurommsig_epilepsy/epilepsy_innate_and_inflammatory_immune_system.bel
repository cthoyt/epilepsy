################################################################################
# Metadata
################################################################################

SET DOCUMENT Name = "Epilepsy - Innate and Inflammatory Immune Response"
SET DOCUMENT Description = "A knowledge assembly of biological entities involved in the innate and inflammatory immune responses and their relation to epilepsy."
SET DOCUMENT Version = "1.0.0"
SET DOCUMENT Authors = "Nora Filep, Anka Gueldenpfennig, Charles Tapley Hoyt, Daniel Domingo-Fernandez"
SET DOCUMENT ContactInfo = "nora.filep@gmail.com"
SET DOCUMENT Copyright = "Copyright © 2012-2018 Fraunhofer SCAI, All rights reserved."
SET DOCUMENT Licenses = "CC BY 4.0"

################################################################################
# Author Credits
################################################################################
# Nora Filep - main curation
# Anka Gueldenpfennig - main curation
# Charles Tapley Hoyt - revision and evaluation
# Daniel Domingo-Fernandez - revision, evaluation, and supervision

################################################################################
# Namespaces
################################################################################

# BEL Framework Namespaces, distributed by SCAI

DEFINE NAMESPACE CHEBI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/chebi/chebi-20170511.belns"
DEFINE NAMESPACE GOBP AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/go-biological-process/go-biological-process-20170511.belns"
DEFINE NAMESPACE HGNC AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/hgnc-human-genes/hgnc-human-genes-20170511.belns"
DEFINE NAMESPACE MESHD AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mesh-diseases/mesh-diseases-20170511.belns"
#DEFINE NAMESPACE MGI AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/mgi-mouse-genes/mgi-mouse-genes-20170511.belns"
#DEFINE NAMESPACE SFAM AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/selventa-protein-families/selventa-protein-families-20170511.belns"
DEFINE NAMESPACE EPT AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/namespace/epilepsy-terminology/epilepsy-terminology-1.0.3.belns"
# BELIEF Namespaces

DEFINE NAMESPACE PTS AS URL "http://belief.scai.fraunhofer.de/openbel/repository/namespaces/PTS.belns"
DEFINE NAMESPACE NIFT AS URL "http://belief.scai.fraunhofer.de/openbel/repository/namespaces/NIFT.belns"
DEFINE NAMESPACE BRCO AS URL "http://belief.scai.fraunhofer.de/openbel/repository/namespaces/BRCO.belns"

################################################################################
# Annotations
################################################################################

# BEL Framework Annotations

#DEFINE ANNOTATION CellLine AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/cell-line/cell-line-20170511.belanno"
DEFINE ANNOTATION Species AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/species-taxonomy-id/species-taxonomy-id-20170511.belanno"


# List Annotations
DEFINE ANNOTATION Subgraph AS URL "https://arty.scai.fraunhofer.de/artifactory/bel/annotation/neurommsig/neurommsig-1.0.3.belanno"

###############################################################################
# Statements
###############################################################################

SET Citation = {"PubMed", "J Mol Neurosci. 2015 Sep;57(1):28-37. doi: 10.1007/s12031-015-0574-x. Epub 2015 May 10.", "25957996"}

SET Evidence = "In this study, we investigated the effect of miR-132, miR-146a, and miR-155 on myeloid-related protein-8 (MRP8)
induced astrocyte-related inflammation. Using quantitative polymerase chain reaction (qPCR) and western blot, we found clear
upregulation of TLR4 and downstream inflammatory cytokines, along with dysregulation of miR-132, miR-146a, and miR-155 in in vitro
astrocytes after exposing them to different concentrations of MRP8"

SET Subgraph = {"miRNA subgraph", "Innate immune system subgraph", "Inflammatory response subgraph"}
# S100A8 = MRP8
p(HGNC:S100A8) -> act(a(BRCO:Astrocyte))
p(HGNC:S100A8) -> bp(PTS:"inflammatory response pathway")
p(HGNC:S100A8) -- r(HGNC:MIR132)
p(HGNC:S100A8) -- r(HGNC:MIR146A)
p(HGNC:S100A8) -- r(HGNC:MIR155)
r(HGNC:MIR132) -- a(BRCO:Astrocyte)
r(HGNC:MIR146A) -- a(BRCO:Astrocyte)
r(HGNC:MIR155) -- a(BRCO:Astrocyte)
p(HGNC:S100A8) -> p(HGNC:TLR4)

SET Evidence = "Together, our findings demonstrate the novel role of TLR4-related microRNAs, especially miR-132, in the regulation of
MRP8-induced astrocyte activation and highlight the importance of miR-132 in the modulation of innate immune response induced by endogenous ligands in neurological diseases."

SET Subgraph = {"miRNA subgraph", "Innate immune system subgraph", "Inflammatory response subgraph"}

r(HGNC:MIR132) -- p(HGNC:TLR4)
r(HGNC:MIR132) -> bp(PTS:"innate immune response pathway")
r(HGNC:MIR132) -- act(a(BRCO:Astrocyte))

SET Evidence = " Our results show that miR-132 is a negative feedback regulator of IL-1β and IL-6, but not TNF-α, by targeting IRAK4. "

SET Subgraph = {"miRNA subgraph", "Innate immune system subgraph", "Inflammatory response subgraph"}
r(HGNC:MIR132) -| p(HGNC:IL1B)
r(HGNC:MIR132) -| p(HGNC:IL6)
r(HGNC:MIR132) causesNoChange p(HGNC:TNF)
r(HGNC:MIR132) -- p(HGNC:IRAK4)

SET Subgraph = {"Innate immune system subgraph", "Inflammatory response subgraph"}

p(HGNC:IRAK4) -- p(HGNC:IL1B)
p(HGNC:IRAK4) -- p(HGNC:IL6)

SET Evidence = "Astrocyte activation, associated with the release of pro-inflammatory cytokines interleukin 1-β (IL-1β), interleukin-6
(IL-6), and tumor necrosis factor alpha (TNF-α), is a hallmark of multiple brain diseases, including mesial temporal lobe epilepsy."

act(a(BRCO:Astrocyte)) -> path(MESHD:"Epilepsy, Temporal Lobe")
act(a(BRCO:Astrocyte)) -> p(HGNC:IL1B)
act(a(BRCO:Astrocyte)) -> p(HGNC:IL6)
act(a(BRCO:Astrocyte)) -> p(HGNC:TNF)

#############################################
SET Citation = {"PubMed", "Trends Pharmacol Sci. 2016 Jun;37(6):463-84. doi: 10.1016/j.tips.2016.03.001. Epub 2016 Apr 6.", "27062228"}

SET Evidence = " As a crucial component of brain innate immunity, neuroinflammation initially contributes to neuronal tissue repair and maintenance.
However, chronic inflammatory processes within the brain and associated blood-brain barrier (BBB) impairment often cause neurotoxicity and
hyperexcitability.Mounting evidence points to a mutual facilitation between inflammation and epilepsy, suggesting that blocking the undesired
inflammatory signaling within the brain might provide novel strategies to treat seizures and epilepsy. Neuroinflammation is primarily characterized by the
upregulation of proinflammatory mediators in epileptogenic foci, among which cyclooxygenase-2 (COX-2)/prostaglandin E2 (PGE2), interleukin-1β
(IL-1β), transforming growth factor-β (TGF-β), toll-like receptor 4 (TLR4), high-mobility group box 1 (HMGB1), and
tumor necrosis factor-α (TNF-α) have been extensively studied."

SET Subgraph = {"Innate immune system subgraph", "Inflammatory response subgraph"}
p(HGNC:PTGS2) -> bp(PTS:"inflammatory response pathway")
p(HGNC:TGFB1) -> bp(PTS:"inflammatory response pathway")
p(HGNC:TLR4) -> bp(PTS:"inflammatory response pathway")
p(HGNC:HMGB1) -> bp(PTS:"inflammatory response pathway")

############################################
SET Citation = {"PubMed", "Mol Neurobiol. 2014 Feb;49(1):337-51. doi: 10.1007/s12035-013-8522-7. Epub 2013 Aug 28.", "23982744"}

SET Evidence = "The role of Toll-like receptor 4 (TLR4) in the activation of innate immunity has been extensively studied in the past several years.
Here, we are the first to report that myeloid-related protein 8 (MRP8), an endogenous TLR4 ligand, is involved in the epileptogenesis of mesial temporal lobe epilepsy (MTLE)"

SET Subgraph = "Innate immune system subgraph"
p(HGNC:TLR4) -> bp(PTS:"innate immune response pathway")
p(HGNC:S100A8) -> bp(EPT:epileptogenesis)
p(HGNC:S100A8) -> path(MESHD:"Epilepsy, Temporal Lobe")
complex(p(HGNC:TLR4), p(HGNC:S100A8)) -- bp(PTS:"innate immune response pathway")

###########################################
SET Citation = {"PubMed", "Clin Lab. 2014;60(2):233-40.", "24660535"}

SET Evidence = "To explore the role of innate immunity in the mechanism of epilepsy, the pentylenetetrazole (PTZ) kindling rat model was established
using intraperitoneal injection of PTZ. The expression levels of TLR2, TLR4, and STAT molecules in rat hippocampi were analyzed using qRT-PCR and
western blotting techniques. The expression levels of SOCS-1 and SOCS-3 in rat hippocampi were analyzed using qRT-PCR.
RESULTS: Our data demonstrated that both the mRNA and protein expression of TLR2 and TLR4 were significantly upregulated in the rat hippocampus
with PTZ injection, which was accompanied by an inhibition of SOCS-1 and SOCS-3 and an upregulation of STAT3."

SET Species = "10116"
SET Subgraph = "Innate immune system subgraph"
p(HGNC:TLR2) positiveCorrelation path(MESHD:Epilepsy)
p(HGNC:SOCS1) negativeCorrelation path(MESHD:Epilepsy)
p(HGNC:SOCS3) negativeCorrelation path(MESHD:Epilepsy)
p(HGNC:STAT3) positiveCorrelation path(MESHD:Epilepsy)
p(HGNC:TLR2) -- a(NIFT:Hippocampus)
p(HGNC:SOCS1) -- a(NIFT:Hippocampus)
p(HGNC:SOCS3) -- a(NIFT:Hippocampus)
p(HGNC:TLR4) -- a(NIFT:Hippocampus)

############################################
SET Citation = {"PubMed", "Nat Commun. 2015 Mar 9;6:6514. doi: 10.1038/ncomms7514.", "25751136"}

SET Evidence = "Furthermore, TLR9 deficiency exacerbated seizure-induced cognitive decline and recurrent seizure severity. Our findings
thus suggest the existence of bidirectional communication between the innate immune and nervous systems for the maintenance of adult brain integrity."
SET Subgraph = "Innate immune system subgraph"
p(HGNC:TLR9) -| bp(EPT:epileptogenesis)
p(HGNC:TLR9) -- path(MESHD:Epilepsy)

#############################################
SET Citation = {"PubMed", "PLoS One. 2012;7(9):e44789. doi: 10.1371/journal.pone.0044789. Epub 2012 Sep 13.", "23028621"}

SET Evidence = " In the present study we investigated the role of miR-146a, a key regulator of the innate immune response,
in the modulation of astrocyte-mediated inflammation."
SET Subgraph = {"Innate immune system subgraph", "Inflammatory response subgraph", "miRNA subgraph"}
r(HGNC:MIR146A) -- bp(PTS:"innate immune response pathway")

SET Evidence = "Modulation of miR-146a expression by transfection of astrocytes with anti-miR146a or mimic, regulated the
mRNA expression levels of downstream targets of miR-146a (IRAK-1, IRAK-2 and TRAF-6) and the expression of IRAK-1 protein."
r(HGNC:MIR146A) -- p(HGNC:IRAK1)
r(HGNC:MIR146A) -- p(HGNC:IRAK2)
r(HGNC:MIR146A) -- p(HGNC:TRAF6)

SET Evidence = "In addition, the expression of IL-6 and COX-2 upon IL-1β stimulation was suppressed by increased levels of miR-146a and increased by the reduction of miR-146a."
r(HGNC:MIR146A) -| p(HGNC:IL6)
r(HGNC:MIR146A) -| p(HGNC:PTGS2)
UNSET Subgraph
SET Subgraph = {"Innate immune system subgraph", "Inflammatory response subgraph"}
p(HGNC:IL1B) -> p(HGNC:IL6)
p(HGNC:IL1B) -> p(HGNC:PTGS2)

#########################################
SET Citation = {"PubMed", "Epilepsia. 2015 Jun;56(6):895-905. doi: 10.1111/epi.12960. Epub 2015 Apr 3.", "25847097"}

SET Evidence = "Small mRNA level increases of CD11b, glial fibrillary acidic protein (GFAP), and IL-1β were found in the
pilocarpine model, consistent with IL-1β contributing to spontaneous seizures. The difference may also be due
to the contribution of peripheral inflammation after pilocarpine injection."

SET Subgraph = "Inflammatory response subgraph"
path(MESHD:Seizures) positiveCorrelation p(HGNC:GFAP)

########################################
SET Citation = {"PubMed", "Seizure. 2015 Mar;26:22-5. doi: 10.1016/j.seizure.2015.01.009. Epub 2015 Jan 21.", "25799897"}

SET Evidence = "The major findings in our study were increased levels of IL-1β, IL-6 and IL-1Ra in epileptic patients
and high levels of IL-1β in TLE group. Our results support the existence of a chronic inflammatory state in epileptic patients."

SET Subgraph = "Inflammatory response subgraph"
p(HGNC:IL1RAP) positiveCorrelation path(MESHD:Epilepsy)
bp(PTS:"inflammatory response pathway") -> path(MESHD:Epilepsy)

#######################################
SET Citation = {"PubMed", "Epilepsia. 2011 Sep;52(9):1645-55. doi: 10.1111/j.1528-1167.2011.03115.x. Epub 2011 Jun 2.", "21635241"}

SET Evidence = "These results suggest that dysregulation of ADK in astrocytes is a common pathologic hallmark of TLE.
Moreover, in vitro data suggest the existence of an additional layer of modulatory crosstalk between the astrocyte-based adenosine cycle and inflammation. "
SET Subgraph = "Inflammatory response subgraph"
p(HGNC:ADK) -- a(BRCO:Astrocyte)
p(HGNC:ADK) positiveCorrelation path(MESHD:"Epilepsy, Temporal Lobe")
p(HGNC:ADK) -- bp(PTS:"inflammatory response pathway")

#######################################
SET Citation = {"PubMed", "JAMA Neurol. 2015 Aug;72(8):882-8. doi: 10.1001/jamaneurol.2015.0941.", "26052981"}

SET Evidence = " Binding of TSPO is increased both ipsilateral and contralateral to seizure foci in patients with TLE,
suggesting ongoing inflammation. Anti-inflammatory therapy may play a role in treating drug-resistant epilepsy."
SET Subgraph = "Inflammatory response subgraph"
p(HGNC:TSPO) positiveCorrelation path(MESHD:"Epilepsy, Temporal Lobe")
p(HGNC:TSPO) -- bp(PTS:"inflammatory response pathway")
bp(PTS:"inflammatory response pathway") -- a(CHEBI:anticonvulsant)

########################################
SET Citation = {"PubMed", "Int J Infect Dis. 2014 Mar;20:52-7. doi: 10.1016/j.ijid.2013.11.006. Epub 2014 Jan 7.", "24406738"}

SET Evidence = "This neurotrophin over-expression is likely to play a key role in the mechanisms of neuronal inflammation
and in the severity of brain damage, particularly in EBV-induced ME."
SET Subgraph = "Inflammatory response subgraph"
p(HGNC:BDNF) -- bp(GOBP:"inflammatory response")


